prototype(Sitegeist.LostInTranslation:Collection.InfoRenderer) < prototype(Neos.Fusion:Component) {
    nodePath = null
    referenceLanguage = null

    documentNode = ${documentNode}
    collectionNode = ${q(this.documentNode).children(this.nodePath).get(0)}

    renderer = Neos.Fusion:Component {
        status = ${Sitegeist.LostInTranslation.compareCollectionWithDimension(props.collectionNode, props.referenceLanguage)}
        documentNode = ${props.documentNode}
        collectionNode = ${props.collectionNode}
        referenceLanguage = ${props.referenceLanguage}

        scriptHref = Neos.Fusion:ResourceUri {
            path = 'resource://Sitegeist.LostInTranslation/Public/Scripts/beinfo.js'
        }

        icon = ${File.readFile('resource://Sitegeist.LostInTranslation/Public/Icons/language.svg')}

        addMissingHref = Neos.Fusion:UriBuilder {
            action="addMissingTranslations"
            package="Sitegeist.LostInTranslation"
            controller="CollectionTranslation"
            @process.addParameter = ${value + '?document=' + String.rawUrlEncode(props.documentNode.contextPath) + '&collection=' + String.rawUrlEncode(props.collectionNode.contextPath) + '&referenceLanguage=' + String.rawUrlEncode(props.referenceLanguage)}
        }

        updateOutdatedHref = Neos.Fusion:UriBuilder {
            action="updateOutdatedTranslations"
            package="Sitegeist.LostInTranslation"
            controller="CollectionTranslation"
            @process.addParameter = ${value + '?document=' + String.rawUrlEncode(props.documentNode.contextPath) + '&collection=' + String.rawUrlEncode(props.collectionNode.contextPath) + '&referenceLanguage=' + String.rawUrlEncode(props.referenceLanguage)}
        }

        renderer = afx`
            <Neos.Fusion:Fragment @if={props.status.missing || props.status.outdated}>
                <script src={props.scriptHref} ></script>
                <lost-in-translation-info
                        icon={props.icon}
                        addMissingHref={props.status.missing ? props.addMissingHref : false}
                        updateOutdatedHref={props.status.outdated ? props.updateOutdatedHref : false}
                >
                    <Neos.Fusion:Fragment @path="attributes.text">
                        <p @if={props.status.missing}>
                            Missing Contents: <Neos.Fusion:Loop items={props.status.missing} @glue=", ">{item.node.label}</Neos.Fusion:Loop>
                        </p>
                        <p @if={props.status.outdated}>
                            Outdated Contents: <Neos.Fusion:Loop items={props.status.outdated}>{item.node.label}</Neos.Fusion:Loop>
                        </p>
                    </Neos.Fusion:Fragment>
                </lost-in-translation-info>
            </Neos.Fusion:Fragment>
        `
    }
}
