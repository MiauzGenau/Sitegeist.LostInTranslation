prototype(Sitegeist.LostInTranslation:Collection.InfoRenderer) < prototype(Neos.Fusion:Component) {
    nodePath = null
    referenceLanguage = null

    documentNode = ${documentNode}
    collectionNode = ${q(this.documentNode).children(this.nodePath).get(0)}

    renderer = Neos.Fusion:Component {
        status = ${Sitegeist.LostInTranslation.compareCollectionWithDimension(props.collectionNode, props.referenceLanguage)}
        documentNode = ${props.documentNode}
        collectionNode = ${props.collectionNode}
        referenceLanguage = ${props.referenceLanguage}

        scriptHref = Neos.Fusion:ResourceUri {
            path = 'resource://Sitegeist.LostInTranslation/Public/Scripts/beinfo.js'
        }

        icon = ${File.readFile('resource://Sitegeist.LostInTranslation/Public/Icons/language.svg')}

        renderer = afx`
            <Neos.Fusion:Fragment @if={props.status.missing || props.status.outdated}>
                <script src={props.scriptHref} ></script>
                <lost-in-translation-info
                        collectionNode={props.collectionNode.contextPath}
                        documentNode={props.documentNode.contextPath}
                        referenceLanguage={props.referenceLanguage}
                        showAddMissingButton={props.status.missing ? true : false}
                        showUpdateOutdatedButton={props.status.outdated ? true : false}
                        icon={props.icon}
                >
                    <Neos.Fusion:Fragment @path="attributes.text">
                        <p @if={props.status.missing}>
                            Missing Contents: <Neos.Fusion:Loop items={props.status.missing} @glue=", ">{item.node.label}</Neos.Fusion:Loop>
                        </p>
                        <p @if={props.status.outdated}>
                            Outdated Contents: <Neos.Fusion:Loop items={props.status.outdated}>{item.node.label}</Neos.Fusion:Loop>
                        </p>
                    </Neos.Fusion:Fragment>
                </lost-in-translation-info>
            </Neos.Fusion:Fragment>
        `
    }
}
