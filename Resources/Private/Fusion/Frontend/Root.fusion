include: **/*.fusion

prototype(Sitegeist.LostInTranslation:CollectionComparisonProcessor) < prototype(Neos.Fusion:Component) {
    referenceDimensions = null

    renderer = Neos.Fusion:Join {
        info = Sitegeist.LostInTranslation:CollectionComparison {
            referenceDimensions = null
            documentNode = ${documentNode}
            collectionNode = ${node}
            referenceDimensions = ${props.referenceDimensions}
        }
        value = ${value}
    }
}

prototype(Sitegeist.LostInTranslation:CollectionComparison) < prototype(Neos.Fusion:Component) {
    nodePath = null
    referenceDimensions = null

    documentNode = ${documentNode}
    collectionNode = ${q(this.documentNode).children(this.nodePath).get(0)}

    renderer = Neos.Fusion:Component {
        status = ${Sitegeist.LostInTranslation.compareCollectionWithDimension(props.collectionNode, props.referenceDimensions)}
        documentNode = ${props.documentNode}
        collectionNode = ${props.collectionNode}
        referenceDimensions = ${props.referenceDimensions}

        scriptHref = Neos.Fusion:ResourceUri {
            path = 'resource://Sitegeist.LostInTranslation/Public/Scripts/beinfo.js'
        }

        icon = ${File.readFile('resource://Sitegeist.LostInTranslation/Public/Icons/language.svg')}

        addMissingHref = Neos.Fusion:UriBuilder {
            action="addMissingTranslations"
            package="Sitegeist.LostInTranslation"
            controller="Node"
            @process.addParameter = ${value + '?document=' + String.rawUrlEncode(props.documentNode.contextPath) + '&collection=' + String.rawUrlEncode(props.collectionNode.contextPath) + '&referenceDimensions=' + Json.stringify(props.referenceDimensions)}
        }

        updateOutdatedHref = Neos.Fusion:UriBuilder {
            action="updateOutdatedTranslations"
            package="Sitegeist.LostInTranslation"
            controller="Node"
            @process.addParameter = ${value + '?document=' + String.rawUrlEncode(props.documentNode.contextPath) + '&collection=' + String.rawUrlEncode(props.collectionNode.contextPath) + '&referenceDimensions=' + Json.stringify(props.referenceDimensions)}
        }

        renderer = afx`
            <Neos.Fusion:Fragment @if={props.status.missing || props.status.outdated}>
                <script src={props.scriptHref} ></script>
                <lost-in-translation-info
                    icon={props.icon}
                    addMissingHref={props.status.missing ? props.addMissingHref : false}
                    updateOutdatedHref={props.status.outdated ? props.updateOutdatedHref : false}
                >
                    <Neos.Fusion:Fragment @path="attributes.text">
                        <Neos.Fusion:Fragment @if={props.status.missing}>
                            Missing Nodes: <Neos.Fusion:Loop items={props.status.missing} @glue=", ">{item.node.label}</Neos.Fusion:Loop>
                        </Neos.Fusion:Fragment>
                        <Neos.Fusion:Fragment @if={props.status.outdated}>
                            Outdated: <Neos.Fusion:Loop items={props.status.outdated}>{item.node.label}</Neos.Fusion:Loop>
                        </Neos.Fusion:Fragment>
                    </Neos.Fusion:Fragment>
                </lost-in-translation-info>
            </Neos.Fusion:Fragment>
        `
    }
}
